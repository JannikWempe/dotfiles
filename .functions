# This line imports an OS specific functions file if one exists
# Not been tested on any OS other than Mac OSX
# TODO: find a better way than uname to distinguish between linux distros
[ -f ".functions.$( uname )" ] && source ".functions.$( uname )"

function kill-port() {
  lsof -i tcp:$1 | awk 'NR!=1 {print $2}' | xargs kill -9
}

# Create a new directory and enter it
function mkcd() {
	mkdir -p "$@" && cd "$_";
}

# Remove python compiled byte-code and mypy/pytest cache in either the current
# directory or in a list of specified directories (including sub directories).
function pyclean() {
    ZSH_PYCLEAN_PLACES=${*:-'.'}
    find ${ZSH_PYCLEAN_PLACES} -type f -name "*.py[co]" -delete
    find ${ZSH_PYCLEAN_PLACES} -type d -name "__pycache__" -delete
    find ${ZSH_PYCLEAN_PLACES} -depth -type d -name ".mypy_cache" -exec rm -r "{}" +
    find ${ZSH_PYCLEAN_PLACES} -depth -type d -name ".pytest_cache" -exec rm -r "{}" +
}

# Wrap git to prefent accidental force pushes by overriding it with --force-with-lease
# see: https://dev.to/jody/default-to-safe-force-pushing-in-git-without-aliases-oi3
function git {
  # Only touch git if it's the push subcommand
  if [[ "$1" == "push" || "$1" == "po" ]]; then
    force=false
    override=false

    for param in "$@"; do
      if [[ $param == "--force" ]]; then force=true; fi
      if [[ $param == "--seriously" ]]; then override=true; fi
    done

    # If we're using --force but not --seriously, change it to --force-with-lease
    if [[ "$force" = true && "$override" = false ]]; then
      echo -e "\033[0;33mDetected use of --force! Using --force-with-lease instead. If you're absolutely sure you can override with --force --seriously.\033[0m"

      # Unset --force
      for param; do
        [[ "$param" = --force ]] || set -- "$@" "$param"; shift
      done

      # Replace it with --force-with-lease
      set -- "push" "$@" "--force-with-lease"; shift
    else
      if [[ "$override" = true ]]; then
        echo -e "\033[0;33mHeads up! Using --force and not --force-with-lease.\033[0m"
      fi

      # Unset --seriously or git will yell at us
      for param; do
        [[ "$param" = --seriously ]] || set -- "$@" "$param"; shift
      done
    fi
  fi

  command git "$@"
}

# This should be the last line of the file
# For local changes
# Don't make edits below this
[ -f ".functions.local" ] && source ".functions.local"
